plugins {
    id 'antlr'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}
group = 'net.joelcourtney'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}
dependencies {
    antlr "org.antlr:antlr4:4.8"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jscience:jscience:4.3.1'

    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
}

generateGrammarSource {
    arguments += ["-no-visitor", "-no-listener"]
}

tasks.compileKotlin.dependsOn(generateGrammarSource)

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = 'doc'
}

jar {
    manifest {
        attributes "Main-Class": "MainKt"
    }
}

shadowJar {
    minimize()
}

task(wrap, type: Exec) {
    dependsOn shadowJar
    executable 'cat'
    args 'src/main/bash/wrapper.sh', 'build/libs/hadron-1.0-SNAPSHOT-all.jar'
    doFirst {
        standardOutput = new FileOutputStream('hadron')
    }
}

task(hadron, type: Exec) {
    dependsOn wrap
    workingDir "."
    commandLine 'chmod', '+x', 'hadron'
}